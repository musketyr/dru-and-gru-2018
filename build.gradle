plugins {
    id 'org.ajoberstar.github-pages' version '1.5.1'
    id 'org.asciidoctor.gradle.asciidoctor' version '1.5.1'
}

String currentVersion = '1.0.0'

version = currentVersion


task exercisesArchive(type: Zip) {
    archiveName = 'gritter.zip'
    from('.') {
        exclude '.git'
        exclude '.idea'
        exclude 'rest'
        exclude '**/.gradle'
        exclude '**/build'
        exclude '**/node_modules'
        exclude 'docs'
        exclude '.gitignore'
        exclude 'README.adoc'
        exclude 'server/src/test'
        exclude 'server/src/integration-test'
    }
}

task copyExercisesArchive(dependsOn: exercisesArchive, type: Copy) {
    into "$asciidoctor.outputDir/html5/archives"
    from exercisesArchive.archivePath
}

// asciidoctor publishing
asciidoctor {

    sourceDir = file('docs')

    resources {
        from(sourceDir) {
            include 'css/**', 'images/**', 'xlsx/**'
        }
    }

    attributes 'docinfo1': ['version': currentVersion],
            'imagesdir': 'images',
            'source-highlighter': 'highlight.js',
            'stylesdir': 'css',
            icons: 'font',
            'toc': 'left',
            version: project.version,
            'projectUrl': 'https://github.com/musketyr/dru-and-gru-2018'
}

githubPages {
    repoUri = 'https://github.com/musketyr/dru-and-gru-2018'

    credentials {
        username = getPropertyOrUseDefault('githubToken', '')
        password = ''
    }

    pages {
        from file(asciidoctor.outputDir.path + '/html5')
    }
}

asciidoctor.dependsOn copyExercisesArchive, ':server:test', ':server:integrationTest', ':hello-world:test'
publishGhPages.dependsOn asciidoctor



String getPropertyOrUseDefault(String propertyName, String defaultValue) {
    hasProperty(propertyName) ? getProperty(propertyName) : defaultValue
}
